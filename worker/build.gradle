apply plugin: 'application'

dependencies {
    compile 'org.apache.curator:curator-recipes:2.7.+'
    compile 'com.google.inject:guice:3.0'
    compile 'com.google.inject.extensions:guice-servlet:3.0'
    compile 'org.eclipse.jetty:jetty-client:9.2.+'
    compile 'org.eclipse.jetty:jetty-server:9.2.+'
    compile 'org.eclipse.jetty:jetty-servlet:9.2.+'
    compile 'org.jsoup:jsoup:1.8.+'
    compile project(':common')
    compile project(':protos')
    compile project(':worker-spi')
}

startScripts {
    classpath += files('conf')
    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\conf', '%APP_HOME%\\conf')
        unixScriptFile.text = unixScriptFile.text.replace('$APP_HOME/lib/conf', '$APP_HOME/conf')
    }
}

applicationDistribution.into('conf') {
    from rootProject.files('conf'), project.files('conf')

}

mainClassName = 'cn.edu.scut.kapok.distributed.worker.WorkerMain'
