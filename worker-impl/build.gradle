apply plugin: 'distribution'
apply plugin: 'jacoco'
apply plugin: 'pmd'
apply plugin: 'findbugs'

tasks.withType(FindBugs) {
    reports {
        html.enabled = true
        xml.enabled = false
    }
}

dependencies {
    compile 'com.google.inject:guice:3.0'
    compile 'org.apache.httpcomponents:httpasyncclient:4.0.2'
    compile 'org.jsoup:jsoup:1.8.+'
    compile project(':worker-core')
    testCompile 'org.mockito:mockito-core:1.10.+'
    testCompile project(':test')
}

sourceSets {
    integTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime
}

task integTest(type: Test) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
}

check.dependsOn integTest

distributions {
    main {
        contents {
            into('lib') {
                from(project.configurations.runtime)
                from(project.tasks[JavaPlugin.JAR_TASK_NAME])
            }
        }
    }
}
